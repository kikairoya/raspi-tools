# Author: kikairoya <kikairoya@gmail.com>
# Build order: ... -> raspi-binutils -> raspi-gcc-stage1 -> raspi-glibc-stage1 -> ...

_target=armv6l-raspi-linux-gnueabihf
_binprefix=${_target}
pkgname=raspi-gcc-stage1
_pkgname=gcc
pkgver=4.8.2
pkgrel=3
pkgdesc="The GNU Compiler Collection (Raspberry Pi)"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org/"
depends=('raspi-binutils>=2.23' 'gcc-libs>=4.8.0' 'libmpc')
options=('!libtool' '!emptydirs' '!strip' '!buildflags' 'staticlibs')
source=(http://ftp.gnu.org/gnu/gcc/${_pkgname}-${pkgver}/gcc-${pkgver}.tar.bz2)

_basedir=${_pkgname}-${pkgver}

build() {
  unset CPPFLAGS

  cd ${srcdir}/${_basedir}

  sed -i '/k prot/agcc_cv_libc_provides_ssp=yes' gcc/configure
  rm -rf libatomic

  echo ${pkgver} > gcc/BASE-VER

  cd ${srcdir}
  rm -rf gcc-build
  mkdir gcc-build && cd gcc-build
  mkdir -p ${_target}/libatomic
  echo install: > ${_target}/libatomic/Makefile

  ${srcdir}/${_basedir}/configure \
    --with-sysroot=/usr/${_target}/sysroot \
    --target=${_target} \
    --prefix=/usr \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --with-newlib \
    --without-headers \
    --disable-linker-build-id \
    --disable-nls \
    --disable-shared \
    --disable-decimal-float \
    --disable-threads \
    --disable-libatomic \
    --disable-libgomp \
    --disable-libitm \
    --disable-libmudflap \
    --disable-libquadmath \
    --disable-libsanitizer \
    --disable-libssp \
    --disable-libstdc++-v3 \
    --enable-languages=c,c++ \
    --disable-lto \
    --with-as=/usr/bin/${_target}-as \
    --with-ld=/usr/bin/${_target}-ld \
    --with-system-zlib \
    --with-arch=armv6 \
    --with-float=hard \
    --with-fpu=vfp

  make
}

package() {
  unset CPPFLAGS
  cd ${srcdir}/gcc-build

  make -j1 DESTDIR=${pkgdir} install

  # Strip debug symbols from libraries; without this, the package size balloons to ~500MB.
  find ${pkgdir}/usr/lib -type f -name "*.a" \
    -exec /usr/bin/${_binprefix}-strip --strip-debug '{}' \;

  # Install Runtime Library Exception
  install -Dm644 ${srcdir}/${_basedir}/COPYING.RUNTIME \
    ${pkgdir}/usr/share/licenses/${pkgname}/RUNTIME.LIBRARY.EXCEPTION

  rm -rf ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/gcc-4.8.2
  rm -rf ${pkgdir}/usr/share/man/man7
  rm -rf ${pkgdir}/usr/share/info
}

md5sums=('a3d7d63b9cb6b6ea049469a0c4a43c9d')
