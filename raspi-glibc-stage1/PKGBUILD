# Author: kikairoya <kikairoya@gmail.com>
# Build order: ... -> raspi-gcc-stage1 -> raspi-glibc-stage1 -> raspi-gcc-stage2 -> ...
# NOTE: valgrind requires rebuilt with each major glibc version

_target=armv6l-raspi-linux-gnueabihf
_binprefix=${_target}
pkgname=raspi-glibc-stage1
pkgver=2.17
pkgrel=2
pkgdesc="GNU C Library (header only, Raspberry Pi)"
arch=(any)
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
groups=('base')
depends=('raspi-linux-api-headers>=3.7')
makedepends=(raspi-binutils 'gcc>=4.7' raspi-gcc-stage1)
options=('!strip' '!libtool' '!buildflags' 'staticlibs')
source=(http://ftp.gnu.org/gnu/libc/glibc-${pkgver}.tar.xz{,.sig}
        glibc-2.18-readdir_r-CVE-2013-4237.patch
        glibc-2.17-strcoll-CVE-2012-4412+4424.patch)
md5sums=('87bf675c8ee523ebda4803e8e1cec638'
         'SKIP'
         '154da6bf5a5248f42a7bf5bf08e01a47'
         'd2313531d75a62c025faafc3e41cdcfa')

prepare() {
  unset CPPFLAGS
  cd ${srcdir}/glibc-${pkgver}

  patch -p1 -i $srcdir/glibc-2.18-readdir_r-CVE-2013-4237.patch
  patch -p1 -i $srcdir/glibc-2.17-strcoll-CVE-2012-4412+4424.patch
  sed -r -i 's/(3..89..)/\1 | 4.*/' configure
  sed -i -e 's/static __m128i/inline &/' sysdeps/x86_64/multiarch/strstr.c

  rm -rf ${srcdir}/glibc-build
  mkdir ${srcdir}/glibc-build
}

build() {
  unset CPPFLAGS
  cd ${srcdir}/glibc-build

  ac_cv_type_long_double=no \
  libc_cv_forced_unwind=yes \
  libc_cv_c_cleanup=yes \
  libc_cv_ctors_header=yes \
  ../glibc-${pkgver}/configure \
    --prefix=/usr/${_target} \
    --libdir=/usr/${_target}/lib \
    --libexecdir=/usr/${_target}/lib/glibc \
    --build=$CHOST \
    --host=${_target} \
    --with-headers=/usr/${_target}/include \
    --enable-add-ons \
    --disable-profile \
    --enable-kernel=2.6.32

  make
}

package() {
  unset CPPFLAGS
  cd ${srcdir}/glibc-build

  make install install_root=${pkgdir}
}
