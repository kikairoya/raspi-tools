# Author: kikairoya <kikairoya@gmail.com>

_target=armv6l-raspi-linux-gnueabihf
_binprefix=${_target}
pkgname=raspi-zlib
pkgver=1.2.8
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP (Raspberry Pi)'
arch=(any)
license=('custom')
url="http://www.zlib.net/"
depends=('raspi-glibc')
makedepends=(raspi-binutils raspi-gcc)
options=('!libtool' '!emptydirs' '!strip' '!buildflags')
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")
md5sums=('44d667c142d7cda120332623eab69f40')

prepare() {
  cd ${srcdir}/zlib-$pkgver
  grep -A 24 '^  Copyright' zlib.h > LICENSE
}

build() {
  unset CPPFLAGS
  cd ${srcdir}/zlib-$pkgver
  CC=${_binprefix}-gcc \
  CXX=${_binprefix}-g++ \
  AR=${_binprefix}-ar \
  AS=${_binprefix}-as \
  LD=${_binprefix}-ld \
  ./configure --prefix=/usr/${_target}
  make
}

package() {
  unset CPPFLAGS
  cd ${srcdir}/zlib-$pkgver
  make install DESTDIR=${pkgdir}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
