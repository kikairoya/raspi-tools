# Author: kikairoya <kikairoya@gmail.com>
# Build order: raspi-linux-api-headers -> raspi-binutils -> raspi-gcc-stage1 -> ...

_target=armv6l-raspi-linux-gnueabihf
_binprefix=${_target}
pkgname=raspi-binutils
pkgver=2.23.2
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (Raspberry Pi)"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.17' 'zlib' 'binutils')
options=('!libtool' '!distcc' '!ccache' '!buildflags')
install=raspi-binutils.install
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.23.2-texinfo-5.0.patch)

_builddir=binutils-build

build() {
  unset CPPFLAGS
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i ${srcdir}/binutils-2.23.2-texinfo-5.0.patch

  cd ${srcdir}
  rm -rf ${_builddir}
  mkdir ${_builddir} && cd ${_builddir}

  ${srcdir}/binutils-${pkgver}/configure \
    --with-sysroot=/usr/${_target}/sysroot \
    --prefix=/usr \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --enable-ld=default \
    --enable-gold \
    --enable-plugins \
    --enable-threads \
    --with-pic \
    --enable-shared \
    --disable-werror \
    --target=${_target} \
    --with-arch=armv6 \
    --with-float=hard \
    --with-fpu=vfp

    make
}

package() {
  unset CPPFLAGS
  cd ${srcdir}/${_builddir}

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/lib/libiberty.a

  for bin in ar as nm objcopy objdump ranlib strip ; do
    rm -f ${pkgdir}/usr/bin/${bin}
  done

  for info in as bfd binutils configure gprof ld standards; do
    test -f ${pkgdir}/usr/share/info/${info}.info && \
    mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/${_binprefix}-${info}.info
  done

  rm -rf ${pkgdir}/usr/share/locale
}

md5sums=('4f8fa651e35ef262edc01d60fb45702e'
         'SKIP'
         '34e439ce23213a91e2af872dfbb5094c')
