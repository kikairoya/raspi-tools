# Author: kikairoya <kikairoya@gmail.com>

_target=armv6l-raspi-linux-gnueabihf
_binprefix=${_target}
pkgname=raspi-openssl
_pkgname=openssl
_ver=1.0.1e
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=4
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (Raspberry Pi)'
arch=(any)
url='https://www.openssl.org'
license=('custom:BSD')
depends=(raspi-zlib)
makedepends=(raspi-binutils raspi-gcc)
options=('!libtool' '!emptydirs' '!strip' '!buildflags')
source=("https://www.openssl.org/source/${_pkgname}-${_ver}.tar.gz"
        "https://www.openssl.org/source/${_pkgname}-${_ver}.tar.gz.asc"
        'no-rpath.patch'
        'ca-dir.patch'
        'openssl-1.0.1e-fix_pod_syntax-1.patch')
md5sums=('66bf6f10f060d561929de96f9dfe5b8c'
         'SKIP'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         '88d3bef4bbdc640b0412315d8d347bdf')

prepare() {
  cd $srcdir/$_pkgname-$_ver

  # remove rpath: http://bugs.archlinux.org/task/14367
  patch -p0 -i $srcdir/no-rpath.patch
  # set ca dir to /etc/ssl by default
  patch -p0 -i $srcdir/ca-dir.patch

  patch -p1 -i $srcdir/openssl-1.0.1e-fix_pod_syntax-1.patch
}

build() {
  cd $srcdir/$_pkgname-$_ver

  export CROSS_COMPILE=${_binprefix}-

  # mark stack as non-executable: http://bugs.archlinux.org/task/12434
  ./Configure \
    --prefix=/usr/${_target} \
    --openssldir=/usr/${_target}/sysroot/etc/ssl \
    shared zlib linux-armv4 \
    -Wa,--noexecstack -march=armv6

  make depend
  make
}

package() {
  cd $srcdir/$_pkgname-$_ver
  make INSTALL_PREFIX=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
  rm -rf $pkgdir/usr/share/man
  rm -rf $pkgdir/usr/share/info
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
